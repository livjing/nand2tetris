// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // Put your code here:
    Mux16(a=preOut, b=addOut, sel=inc, out=o1);  // o1 = (preOut or addOut)

    Mux16(a=o1, b=in, sel=load, out=o2); // o2 = ((preOut or addOut) or in)

    Mux16(a=o2, b=false, sel=reset, out=o3); // o3 = ((preOut or addOut) or in) or false

    // 前3次操作只是得到了真正需要输入的in到底是哪个

    Register(in=o3, load=true, out=out, out=preOut); // let out = in

    Inc16(in=preOut, out=addOut);  // out+=1
}
